apply plugin: 'com.android.application'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 5
        versionPatch 5
    }

    outputOptions {
        renameOutput true
        nameFormat '${appName}-${buildType}-${versionName}'
    }
}
Properties funstream = new Properties()
Properties fabric = new Properties()
try {
    funstream.load(file('funstream.properties').newDataInputStream())
    fabric.load(file('fabric.properties').newDataInputStream())
}
catch (Exception ex) {
    throw new GradleException("Missing .properties, check files");
}

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
        }
        debug {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.slx.funstream.chat"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        buildConfigField "String", "CONSUMER_KEY", "\"${fabric.getProperty("apiKey")}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${fabric.getProperty("apiSecret")}\""
        buildConfigField "String", "TOKEN", "\"${funstream.getProperty("token")}\""
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            //ext.enableCrashlytics = false
            buildConfigField "String", "APP_KEY", "\"${funstream.getProperty("app_key")}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "APP_KEY", "\"${funstream.getProperty("app_key")}\""
        }
    }

    productFlavors {
        full {
            applicationId = "com.slx.funstream.client"
            versionName advancedVersioning.versionName + "-client"
            versionCode advancedVersioning.versionCode
        }
        chat {
            applicationId = "com.slx.funstream.chat"
            versionCode advancedVersioning.versionCode
            versionName advancedVersioning.versionName
        }
    }

    repositories{
        maven {
            url 'http://dl.bintray.com/amulyakhare/maven'
        }
    }
}

ext {
    support_lib_version = "25.3.1"
    dagger_version = "2.10"
    butterknife_version = "8.5.0"
    retrofit_version = "2.2.0"
    okhttp_version = "3.4.1"
    gson_version = "2.7"
    rxjava_version = "2.0.7"
    rxandroid_version = "2.0.1"
    rxlifecycle_version = "2.0.1"
    rxbinding_version = "2.0.0"
    crashlytics_version = "2.6.5@aar"
    leakcanary_version = "1.5"
    stetho_version = "1.4.1"
    espresso_version = "2.2.2"
    espresso_rules_version = "0.5"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:recyclerview-v7:${support_lib_version}"
    compile "com.android.support:cardview-v7:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"

    // DI
    compile "com.jakewharton:butterknife:${butterknife_version}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"
    compile "com.google.dagger:dagger:${dagger_version}"
    annotationProcessor "com.google.dagger:dagger-compiler:${dagger_version}"
    compile "com.google.dagger:dagger-android-support:${dagger_version}"
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // Network
    compile "com.squareup.retrofit2:retrofit:${retrofit_version}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${retrofit_version}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    compile "com.google.code.gson:gson:${gson_version}"

    compile ('io.socket:engine.io-client:0.8.3') {
        exclude group: 'org.json', module: 'json'
    }
    compile files('libs/socket.io-client-0.8.4-SNAPSHOT.jar')

    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    // JWT
    compile 'com.nimbusds:nimbus-jose-jwt:3.10'

    // rx
    compile "io.reactivex.rxjava2:rxandroid:${rxandroid_version}"
    compile "io.reactivex.rxjava2:rxjava:${rxjava_version}"
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    compile "com.trello.rxlifecycle2:rxlifecycle:${rxlifecycle_version}"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:${rxlifecycle_version}"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:${rxlifecycle_version}"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:${rxbinding_version}"
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${rxbinding_version}"

    // debug
    compile "com.facebook.stetho:stetho:${stetho_version}"
    compile "com.facebook.stetho:stetho-okhttp3:${stetho_version}"

    compile("com.crashlytics.sdk.android:crashlytics:${crashlytics_version}") {
        transitive = true;
    }

    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanary_version}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"

    fullCompile 'com.google.android.exoplayer:exoplayer:r2.1.0'

    compile 'com.android.support:multidex:1.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.easytesting:fest:1.0.16'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging',
                module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents',
                module: 'httpclient'
    }

//    androidTestCompile ("com.android.support.test.espresso:espresso-core:${espresso_version}") {
//        exclude group: 'com.android.support', module: 'appcompat'
//        exclude group: 'com.android.support', module: 'support-v4'
//        exclude group: 'com.android.support', module: 'support-annotations'
//    }
//    androidTestCompile "com.android.support.test:runner:${espresso_rules_version}"
//    androidTestCompile "com.android.support.test:rules:${espresso_rules_version}"
//    androidTestCompile "com.android.support:support-annotations:${support_lib_version}"
//    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espresso_version}") {
//        exclude group: 'com.android.support', module: 'appcompat'
//        exclude group: 'com.android.support', module: 'support-v4'
//        exclude group: 'com.android.support', module: 'support-annotations'
//        exclude module: 'recyclerview-v7'
//    }
}

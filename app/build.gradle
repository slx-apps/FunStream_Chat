apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'me.tatarka.retrolambda'

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 4
        versionPatch 0
    }

    outputOptions {
        renameOutput true
        nameFormat '${appName}-${buildType}-${versionName}'
    }
}

Properties funstream = new Properties()
try {
    funstream.load(file('funstream.properties').newDataInputStream())
}
catch (Exception ex) {
    throw new GradleException("Missing funstream.properties, check the APP_KEY");
}

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
        }
        debug {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "com.slx.funstream.chat"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName

    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
//            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_KEY", "\"${funstream.getProperty("app_key")}\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "APP_KEY", "\"${funstream.getProperty("app_key")}\""
        }
    }

    productFlavors {
        full {
            applicationId = "com.slx.funstream.client"
            versionName advancedVersioning.versionName + "-client"
            versionCode 1
        }
        chat {
            applicationId = "com.slx.funstream.chat"
            versionCode advancedVersioning.versionCode
            versionName advancedVersioning.versionName
        }
    }

}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    // DI
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // Network
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.1.0'
    compile 'com.google.code.gson:gson:2.5'
    // WS
    // compile 'org.java-websocket:Java-WebSocket:1.3.0'
    //    compile('io.socket:socket.io-client:0.6.1') {
    //        exclude group: 'org.json', module: 'json'
    //    }
    compile 'io.socket:engine.io-client:0.6.2'
    compile files('libs/socket.io-client-0.6.3-SNAPSHOT.jar')
//    compile ('io.socket:socket.io-client:0.7.0') {
//        // excluding org.json which is provided by Android
//        exclude group: 'org.json', module: 'json'
//    }
    // JWT
    compile 'com.nimbusds:nimbus-jose-jwt:3.10'
    // RW
    //compile 'com.marshalchen.ultimaterecyclerview:library:0.3.18'
    // rx
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //specific for Full client version
    fullCompile project(':vitamio')
    //compile 'com.scottyab:secure-preferences-lib:0.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.2.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.easytesting:fest:1.0.16'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile('org.robolectric:robolectric:3.0-rc3') {
        exclude group: 'commons-logging',
                module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents',
                module: 'httpclient'
    }


    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}
